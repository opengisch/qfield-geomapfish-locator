import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtCore
import org.qfield
import org.qgis
import Theme

Item {
  id: plugin
  property var mainWindow: iface.mainWindow()
  property var mapCanvas: iface.mapCanvas()

  readonly property var presets: [
    { name: "GeoMapFish Demo", url: "https://geomapfish-demo-2-9.camptocamp.com/search", crs: "EPSG:2056" },
    { name: "SIGIP", url: "https://www.sigip.ch/search", crs: "EPSG:2056" },
    { name: "Cartoriviera", url: "https://map.cartoriviera.ch/search", crs: "EPSG:2056" },
    { name: "SITN", url: "https://sitn.ne.ch/search", crs: "EPSG:2056" }
  ]

  function getPresetByName(name) {
    return presets.find(p => p.name === name);
  }

  function getActivePreset() {
    return getPresetByName(settings.service_endpoint) || presets[0];
  }

  Component.onCompleted: {
    geoMapFishLocatorFilter.locatorBridge.registerQFieldLocatorFilter(geoMapFishLocatorFilter);
  }

  Component.onDestruction: {
    geoMapFishLocatorFilter.locatorBridge.deregisterQFieldLocatorFilter(geoMapFishLocatorFilter);
  }

  Settings {
    id: settings
    category: "qfield-geomapfish-locator"

    property string service_endpoint: "GeoMapFish Demo"
    property string service_crs: ""
    property string service_custom_url: ""
    property string service_custom_url_history: "[]"
  }

  function configure() {
    settingsDialog.open();
  }

  QFieldLocatorFilter {
    id: geoMapFishLocatorFilter
    name: "GeoMapFish"
    displayName: "GeoMapFish"
    prefix: "gmf"
    locatorBridge: iface.findItemByObjectName('locatorBridge')
    parameters: {
      "service_custom_url": settings.service_custom_url || plugin.getActivePreset().url,
      "service_crs": settings.service_crs || plugin.getActivePreset().crs
    }
    source: Qt.resolvedUrl('geomapfish.qml')

    Component.onCompleted: {
      if (geoMapFishLocatorFilter.description !== undefined) {
        geoMapFishLocatorFilter.description = "Returns GeoMapFish search results."
      }
    }

    function triggerResult(result) {
      if (result.userData.type === Qgis.GeometryType.Point) {
        const centroid = GeometryUtils.reprojectPoint(
          GeometryUtils.centroid(result.userData),
          CoordinateReferenceSystemUtils.fromDescription(parameters["service_crs"]),
          mapCanvas.mapSettings.destinationCrs
        )
        mapCanvas.mapSettings.setCenter(centroid, true);
      } else {
        const extent = GeometryUtils.reprojectRectangle(
          GeometryUtils.boundingBox(result.userData),
          CoordinateReferenceSystemUtils.fromDescription(parameters["service_crs"]),
          mapCanvas.mapSettings.destinationCrs
        )
        mapCanvas.mapSettings.setExtent(extent, true);
      }

      locatorBridge.geometryHighlighter.qgsGeometry = result.userData;
      locatorBridge.geometryHighlighter.crs = CoordinateReferenceSystemUtils.fromDescription(parameters["service_crs"]);
    }

    function triggerResultFromAction(result, actionId) {
      if (actionId === 1) {
        let navigation = iface.findItemByObjectName('navigation')
        const centroid = GeometryUtils.reprojectPoint(
          GeometryUtils.centroid(result.userData),
          CoordinateReferenceSystemUtils.fromDescription(parameters["service_crs"]),
          mapCanvas.mapSettings.destinationCrs
        )
        navigation.destination = centroid
      }
    }
  }

  Dialog {
    id: settingsDialog
    parent: mainWindow.contentItem
    visible: false
    modal: true
    font: Theme.defaultFont
    standardButtons: Dialog.Ok | Dialog.Cancel
    title: qsTr("GeoMapFish search settings")
    x: (mainWindow.width - width) / 2
    y: (mainWindow.height - height) / 2
    width: mainWindow.width * 0.8

    property var urlHistory: []

    function loadHistory() {
      try {
        urlHistory = JSON.parse(settings.service_custom_url_history);
        if (!Array.isArray(urlHistory)) urlHistory = [];
      } catch (e) {
        urlHistory = [];
      }
    }

    function saveToHistory(url, crs) {
      urlHistory = urlHistory.filter(e => e.url !== url);
      urlHistory.unshift({ url: url, crs: crs });
      if (urlHistory.length > 10) urlHistory.length = 10;
      settings.service_custom_url_history = JSON.stringify(urlHistory);
    }

    function deleteFromHistory() {
      const url = customUrlCombo.editText.trim();
      urlHistory = urlHistory.filter(e => e.url !== url);
      settings.service_custom_url_history = JSON.stringify(urlHistory);
      updateCustomUrlCombo();
    }

    function updateCustomUrlCombo() {
      customUrlCombo.model = urlHistory.map(e => e.url);
    }

    function updateUI() {
      const isCustom = endpointCombo.currentText === qsTr("Custom");

      customUrlRow.visible = isCustom;

      if (isCustom) {
        Qt.callLater(() => {
          customUrlCombo.forceActiveFocus();
          Qt.inputMethod.show();
        });
      }

      if (isCustom) {
        updateCustomUrlCombo();
        const currentUrl = customUrlCombo.editText.trim();
        const saved = urlHistory.find(e => e.url === currentUrl);
        serviceCrsTextField.text = saved ? saved.crs : (settings.service_crs || plugin.getActivePreset().crs);
      } else {
        const preset = plugin.getPresetByName(endpointCombo.currentText);
        if (preset) {
          serviceCrsTextField.text = preset.crs;
        }
      }
    }

    onOpened: {
      loadHistory();

      let endpointItems = presets.map(p => p.name);
      endpointItems.push(qsTr("Custom"));
      endpointCombo.model = endpointItems;

      const isCustom = settings.service_custom_url !== "";
      if (isCustom) {
        endpointCombo.currentIndex = endpointCombo.model.length - 1;
        customUrlCombo.editText = settings.service_custom_url;
      } else {
        const idx = endpointCombo.model.indexOf(settings.service_endpoint);
        endpointCombo.currentIndex = idx !== -1 ? idx : 0;
      }

      updateUI();
    }

    ColumnLayout {
      width: parent.width
      spacing: 10

      Label {
        id: serviceUrlLabel
        text: qsTr("Service URL")
        font: Theme.defaultFont
      }

      ComboBox {
        id: endpointCombo
        Layout.fillWidth: true
        font: Theme.defaultFont
        onActivated: settingsDialog.updateUI()
      }

      RowLayout {
        id: customUrlRow
        Layout.fillWidth: true
        spacing: 8
        visible: false

        ComboBox {
          id: customUrlCombo
          Layout.fillWidth: true
          font: Theme.defaultFont
          editable: true
          onEditTextChanged: settingsDialog.updateUI()
        }

        QfToolButton {
          id: deleteButton
          bgcolor: "transparent"
          iconSource: Theme.getThemeVectorIcon("ic_delete_forever_white_24dp")
          iconColor: Theme.mainTextColor
          onClicked: {
            settingsDialog.deleteFromHistory();
            mainWindow.displayToast(qsTr("Removed URL"));
          }
        }
      }

      Label {
        id: serviceCrsLabel
        text: qsTr("Service CRS")
        font: Theme.defaultFont
      }

      TextField {
        id: serviceCrsTextField
        Layout.fillWidth: true
        font: Theme.defaultFont
        placeholderText: qsTr("e.g., EPSG:2056")
      }
    }

    onAccepted: {
      const crs = serviceCrsTextField.text.trim();

      if (!crs) {
        mainWindow.displayToast(qsTr("CRS is required"));
        return;
      }

      const isCustom = endpointCombo.currentText === qsTr("Custom");

      if (isCustom) {
        const url = customUrlCombo.editText.trim();
        if (!url) {
          mainWindow.displayToast(qsTr("URL is required"));
          return;
        }
        if (!url.startsWith("http://") && !url.startsWith("https://")) {
          mainWindow.displayToast(qsTr("Invalid URL entered"));
          return;
        }

        settings.service_endpoint = "Custom";
        settings.service_custom_url = url;
        settings.service_crs = crs;
        saveToHistory(url, crs);
        mainWindow.displayToast(qsTr("Settings stored"));
      } else {
        const preset = plugin.getPresetByName(endpointCombo.currentText);
        settings.service_endpoint = endpointCombo.currentText;
        settings.service_custom_url = "";
        settings.service_crs = crs;

        if (preset && crs !== preset.crs) {
          mainWindow.displayToast(qsTr("Preset CRS changed"));
        } else {
          mainWindow.displayToast(qsTr("Settings stored"));
        }
      }
    }
  }
}
